# This policy requires resources of type `awscc_cloudtrail_trail` to have cloudwatch log group arn set.

# Copyright (c) HashiCorp, Inc.
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfconfig/v2" as tfconfig
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"resource_awscc_cloudtrail": "awscc_cloudtrail_trail",
	"message":                   "Attribute 'cloud_watch_logs_group_arn' must be present for 'awscc_cloudtrail_trail' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/cloudtrail-controls.html#cloudtrail-4 for more details.",
	"policy_name":               "cloudtrail-cloudwatch-logs-group-arn-present",
	"constant_value":            "constant_value",
	"logs_group_arn":            "cloud_watch_logs_group_arn",
}

# Variables

resources = tf.config(tfconfig.resources).type(const.resource_awscc_cloudtrail).resources
violations = collection.reject(resources, func(res) {
	key_path = "config." + const.logs_group_arn
	return maps.get(res, key_path, false) is not false and
		maps.get(res, key_path + "." + const.constant_value, false) is not ""
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
